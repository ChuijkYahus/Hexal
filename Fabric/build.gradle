plugins {
    id 'fabric-loom' version "$loomVersion"
    id 'hexal.platform'
}

archivesBaseName = getArtifactID("fabric")

loom {
    mixin.defaultRefmapName = "hexal.mixins.refmap.json"

    accessWidenerPath = file("src/main/resources/hexal.accesswidener")

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            runDir "run"
        }
        server {
            server()
            setConfigName("Fabric Server")
            runDir "runServer"
        }
        datagen {
            client()
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=${modID}"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
        }

        configureEach {
            ideConfigGenerated(true)
        }
    }
}

repositories {
    mavenCentral()

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so:
    flatDir {
        dir 'libs'
    }

    maven {
        name = "Ladysnake Mods"
        url = 'https://maven.ladysnake.org/releases'
    }
    maven {
        name "entity reach"
        url "https://maven.jamieswhiteshirt.com/libs-release/"
    }
    maven { url "https://mvn.devos.one/snapshots/" }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        url 'https://repo.sleeping.town'
        content {
            includeGroup 'com.unascribed'
        }
    }

    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings loom.officialMojangMappings()
    modImplementation("net.fabricmc:fabric-language-kotlin:${fabricLanguageKotlinVersion}")
    modImplementation "net.fabricmc:fabric-loader:${fabricLoaderVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabricVersion}"

    // Reqs
    compileOnly "com.demonwav.mcdev:annotations:1.0"

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compileOnly project(":Common")

    modImplementation "at.petra-k.paucal:paucal-fabric-$minecraftVersion:$paucalVersion"
    modImplementation("at.petra-k.hexcasting:hexcasting-fabric-$minecraftVersion:$hexcastingVersion") {
        exclude module: "phosphor"
    }
    modImplementation "ram.talia.moreiotas:moreiotas-fabric-$minecraftVersion:$moreIotasVersion"
    modImplementation "vazkii.patchouli:Patchouli:$minecraftVersion-$patchouliVersion-FABRIC-SNAPSHOT"
    modImplementation "com.samsthenerd.inline:inline-fabric:$minecraftVersion-$inlineVersion"

    modImplementation "me.zeroeightsix:fiber:$fiberVersion"
    include "me.zeroeightsix:fiber:$fiberVersion"

    // Hexal and MoreIotas depend on "cardinal-components", so we need this specific jar for the dev env to work
    modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-api:$cardinalComponentsVersion") {
        transitive false // don't pull in all of the extra CC sub-jars that we don't need
    }
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:$cardinalComponentsVersion"
//    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-util:$cardinalComponentsVersion"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:$cardinalComponentsVersion"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-item:$cardinalComponentsVersion"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-block:$cardinalComponentsVersion"

    modImplementation "$modID:serialization-hooks:$serializationHooksVersion"
    include "$modID:serialization-hooks:$serializationHooksVersion"
    implementation(include("com.github.LlamaLad7:MixinExtras:0.1.1"))

    modImplementation "software.bernie.geckolib:geckolib-fabric-$minecraftVersion:$geckolibVersion"

    // Optional integrations

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:$reiVersion"

    modApi("me.shedaniel.cloth:cloth-config-fabric:$clothConfigVersion") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation "dev.emi:emi-fabric:${emiVersion}"

    modApi("com.github.Virtuoel:Pehkui:${pehkuiVersion}", {
        exclude group: "net.fabricmc.fabric-api"
    })

//    modApi include("com.unascribed:lib39-core:$lib39Version+$minecraftVersion")
//    modApi include("com.unascribed:lib39-phantom:$lib39Version+$minecraftVersion")

    modImplementation("com.terraformersmc:modmenu:$modmenuVersion")

    implementation 'org.jetbrains:annotations:23.0.0'

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}
compileKotlin {
    source(project(":Common").sourceSets.main.kotlin)
}

sourcesJar {
    from project(":Common").sourceSets.main.allJava
}

sourceSets {
    main.resources.srcDirs += ['src/generated/resources', '../Common/src/generated/resources']
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.register("ciArtifacts", Copy) {
    from remapJar
    into rootProject.file("build/ciArtifacts")
}

build {
    dependsOn ciArtifacts
}

/*
modrinth {
    token = System.env.MODRINTH_TOKEN
    projectId = "$modID" // This can be the project ID or the slug. Either will work!
    versionNumber = "$modVersion" // You don't need to set this manually. Will fail if Modrinth has this version already
    versionName = "$modVersion-fabric"
    changelog = getLatestChangelog()

    versionType = "release" // This is the default -- can also be `beta` or `alpha`
    uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    gameVersions = ["1.20.1"] // Must be an array, even with only one version
    loaders = ["fabric"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
    dependencies { // A special DSL for creating dependencies
        // scope.type
        // The scope can be `required`, `optional`, `incompatible`, or `embedded`
        // The type can either be `project` or `version`
        required.project "hex-casting" // Creates a new required dependency on Hex Casting
        required.project "paucal"
        required.project "patchouli"
        required.project "fabric-language-kotlin"
        required.project "cardinal-components-api"
        required.project "geckolib"
    }
}
 */

hexalModDependencies {
    requires("fabric-api")
    requires("fabric-language-kotlin")
    requires("cardinal-components-api")
    requires("patchouli")
    requires("cloth-config")
    requires("paucal")
    requires(curseforge: "hexcasting", modrinth: "hex-casting")
    requires("moreiotas")
    requires("geckolib")

    optional("modmenu")
    optional("pehkui")
}

publishMods {
    file = remapJar.archiveFile
}

setupJar(this)
